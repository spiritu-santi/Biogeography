#####################################
# GTR+G nucleotide substitution model #
#####################################

### The stationary frequencies and exchangeability rates are drawn from uniform Dirichlet distributions ###
sf_hp <- v(1,1,1,1)
sf ~ dnDirichlet(sf_hp)
er_hp <- v(1,1,1,1,1,1)
er ~ dnDirichlet(er_hp)
moves[mvi++] = mvSimplexElementScale(er, alpha=10.0, weight=5.0)
moves[mvi++] = mvSimplexElementScale(sf, alpha=10.0, weight=5.0)

### Create the matrix ###
Q_cytb := fnGTR(er,sf)

### Create a discretized gamma distribution of rate categories, with the shape parameter being drawn from an exponential prior ###
alpha_cytb ~ dnExponential(1.0)
moves[mvi++] = mvScale(alpha_cytb, lambda=0.01, weight=5)
moves[mvi++] = mvScale(alpha_cytb, lambda=0.1,  weight=1.0)
rates_cytb := fnDiscretizeGamma( alpha_cytb, alpha_cytb, 4 )

### Create the substitution model and clamp with our observed DNA data ###
phySeq ~ dnPhyloCTMC(tree=fbd_tree, Q=Q_cytb, siteRates=rates_cytb, branchRates=branch_rates, type="DNA")
phySeq.clamp(cytb)
